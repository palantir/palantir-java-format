apply plugin: 'java-gradle-plugin'
apply plugin: 'groovy'
apply from: rootProject.file('gradle/publish-jar.gradle')

dependencies {
    implementation gradleApi()
    implementation 'com.google.guava:guava'
    implementation 'gradle.plugin.org.jetbrains.gradle.plugin.idea-ext:gradle-idea-ext'
    implementation project(':palantir-java-format-utils')

    // it would be nicer if we could only depend on some small API and service load the 'logic', but many plumbing
    // utilities are currently in this (e.g. applying Replacements, lineRangesToCharRanges etc)
    implementation project(':palantir-java-format')

    testImplementation 'com.netflix.nebula:nebula-test'
}

gradlePlugin {
    automatedPublishing = false
    plugins {
        palantirJavaFormat {
            id = 'com.palantir.java-format'
            implementationClass = 'com.palantir.javaformat.gradle.PalantirJavaFormatPlugin'
            description = 'A modern, lambda-friendly, 120 character Java formatter.'
        }
        palantirJavaFormatIdea {
            id = 'com.palantir.java-format-idea'
            implementationClass = 'com.palantir.javaformat.gradle.PalantirJavaFormatIdeaPlugin'
            description = 'Plugin to configure the PalantirJavaFormat IDEA plugin based on an optional implementation version of the formatter.'
        }
        palantirJavaFormatProvider {
            id = 'com.palantir.java-format-provider'
            implementationClass = 'com.palantir.javaformat.gradle.PalantirJavaFormatProviderPlugin'
            description = 'Exposes a configuration containing the palantir-java-format jarss'
        }
    }
}

idea {
    module {
        sourceDirs += sourceSets.main.groovy.srcDirs
    }
}
