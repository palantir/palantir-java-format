/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-library'
    id 'dev.equo.p2deps' version '1.7.3'
}

p2deps {
    into 'compileOnly', {
        p2repo 'https://download.eclipse.org/releases/2020-09/202009161000/'

        install 'org.eclipse.jdt.core'
    }
}

dependencies {
    implementation project(':palantir-java-format')
}

jar {
    archiveBaseName = 'palantir-java-format-eclipse-plugin'
    manifest {
        from 'src/main/resources/META-INF/MANIFEST.MF'
    }
    // We embed some dependencies into the JAR file
    from(configurations.runtimeClasspath.filter {
        var name = it.getName()
        name.startsWith('functionaljava') || name.startsWith('guava') || name.startsWith('palantir')
    }) {
        into 'lib'
        // The libraries are listed without a version in the manifest
        rename('(.*)-[0-9b.]+(\\.dirty|-jre)?\\.jar', '$1.jar')
    }
}
